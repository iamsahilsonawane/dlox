# Grammer for dlox

### Syntax Grammer
program          → declaration* EOF ;
declaration      → varDecl | statement;
varDecl          → "var" IDENTIFIER ("=" expression ";")? ;
statement        → exprStmt | printStmt | ifExpr
                   | whileStmt | forStmt | block | break;
breakStmt        → "break" ";" ; 
ifExpr           → "if" "(" expression ")" statement
                   ( "else" statement )? ;
forStmt          → "for" "(" (varDecl | exprStmt | ";") 
                  expression? ";"
                  expression? ")" statement ;
whileStmt        → "while" "(" expression ")" statement ;
block            → "{" declaration* "}" ;
exprStmt         → expression ";" ;
printStmt        → "print" expression ";" ;

expression       → assignment ;
assignment       → IDENTIFIER "=" assignment | conditional ;
conditional      → logic_or ( "?" expression ":" expression)* ;
logic_or         → logic_and ( "or" logic_and )* ;
logic_and        → equality ( "and" equality )*;
equality         → comparison ( ( "!=" | "==" ) comparison )* ;
comparison       → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term             → factor ( ( "-" | "+" ) factor )* ;
factor           → unary ( ( "/" | "*" ) unary )* ;
unary            → ( "!" | "-" | "--" | "++" ) unary
                 | postfix ;
postfix          → primary ( "--" | "++" )* ;
primary          → NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" 
                 | IDENTIFIER
                 //error productions 
                 | ( "!=" | "==" ) equality
                 | ( ">" | ">=" | "<" | "<=" ) comparison
                 | ( "+" ) term
                 | ( "/" | "*" ) factor ;
